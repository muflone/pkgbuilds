# Maintainer: Muflone http://www.muflone.com/contacts/english/
# Contributor: Arne Hoch <arne@derhoch.de>

pkgname=dbeaver
pkgver=25.2.0
pkgrel=1
_COMMON_COMMIT_ID='841df28403631384669138883b8a3550ddd07f88'
_JDBC_LIBSQL_COMMIT_ID='db7a8052b99ade708e45955807e528af8c1ee13a'
pkgdesc="Free universal SQL Client for developers and database administrators (community edition)"
arch=('x86_64')
url="https://dbeaver.io/"
license=("Apache-2.0")
depends=('java-runtime>=21' 'gtk3' 'gtk-update-icon-cache' 'libsecret')
makedepends=('maven' 'java-environment>=21')
conflicts=('dbeaver-plugin-sshj-lib' 'dbeaver-plugin-apache-poi' 'dbeaver-plugin-office')
replaces=('dbeaver-plugin-sshj-lib' 'dbeaver-plugin-apache-poi' 'dbeaver-plugin-office')
source=("${pkgname}-${pkgver}.tar.gz"::"https://github.com/dbeaver/dbeaver/archive/${pkgver}.tar.gz"
        "dbeaver-common-${pkgver}.tar.gz"::"https://github.com/dbeaver/dbeaver-common/archive/${_COMMON_COMMIT_ID}.tar.gz"
        "dbeaver-jdbc-libsql-${pkgver}.tar.gz"::"https://github.com/dbeaver/dbeaver-jdbc-libsql/archive/${_JDBC_LIBSQL_COMMIT_ID}.tar.gz"
        "https://repo.maven.apache.org/maven2/org/apache/commons/commons-compress/1.28.0/commons-compress-1.28.0.jar"
        "io.${pkgname}.DBeaver.desktop"
        "${pkgname}.sh"
        "${pkgname}.profile.gz"
        "${pkgname}.hook"
        "${pkgname}.install"
        "2a5d4c8.patch"::"https://github.com/dbeaver/dbeaver/commit/2a5d4c8dbdbdf8a85bd47265f29642519ad6b25e.diff"
        "c15e81f.patch"::"https://github.com/dbeaver/dbeaver/commit/c15e81f007bcd9f3b2355728d0eaf7906d4ef125.diff")
sha256sums=('90b470e819b723b4461ddac395bba74dedd53103f5dd534cf6bb35b862c7cc10'
            '1015f173777a094ea4afef6cfd8ddee76910a0599d7e036019563e402a6eb785'
            '90b17ab3f6be91e8c3043091fe8c3ba4fcce7fe63152955013067e35b511a901'
            'e1522945218456f3649a39bc4afd70ce4bd466221519dba7d378f2141a4642ca'
            '9480a7d08f680e10c399db070c5a04cbabf282442602a2ef83d1159fe7c3e88b'
            '406a2980806c394670e88b1ae70134900be376c2ea4a4216610591cc8b557526'
            '1863e74bdcf22b7328e6e8487cbebff7d5360e34bde85c1dd226b168b4737034'
            'f8b763ca210bfa4d9a4e407b656ba4f5d1bf2f3f54c67044f7a4dd0c3625fc22'
            'f8d65dd933049b587a5815ea75a30ef944300b812df383ca1c2dcd68280bc7ab'
            '586a37d31ca5b943ea5384ac8332a4a9ed26641c311dd8e2d74dd3935521b0bb'
            'f333ae86562d8dc2d24e0d55833d871eec6596c58317bfa19dd18fb6fed0059c')
install="${pkgname}.install"

prepare() {
  # Fix version number in profile file
  gzip --decompress --keep --stdout "${pkgname}.profile.gz" | 
    sed "s/DBEAVER_VERSION/${pkgver}/g" |
    gzip -9 > "${pkgname}.profile-${pkgver}.gz"

  # Fix dependencies path
  ln -sf dbeaver-common-${_COMMON_COMMIT_ID} dbeaver-common
  ln -sf dbeaver-jdbc-libsql-${_JDBC_LIBSQL_COMMIT_ID} dbeaver-jdbc-libsql

  cd "${srcdir}/${pkgname}-${pkgver}"

  # Backport fixes
  patch -p1 -i "${srcdir}/2a5d4c8.patch"
  patch -p1 -i "${srcdir}/c15e81f.patch"
  # Download dependencies during prepare FS#55873
  # https://bugs.archlinux.org/task/55873
  export MAVEN_OPTS="-Xmx2048m"
  mvn --batch-mode -T 1C validate
}

build() {
  cd "${pkgname}-${pkgver}"
  pushd "product/aggregate"
  mvn --batch-mode -Djdk.xml.maxGeneralEntitySizeLimit=0 -Djdk.xml.totalEntitySizeLimit=0 -T 1C package
  popd
  # Add missing dependency org.apache.commons.commons-compress
  install -m 644 "${srcdir}/commons-compress-1.28.0.jar" \
    "product/repositories/org.jkiss.dbeaver.ce.repository/target/repository/plugins/org.apache.commons.commons-compress_1.28.0.jar"
}

package() {
  cd "${pkgname}-${pkgver}/product/community"
  # Install icons into /usr/share/icons/hicolor
  for _size in 16 32 48 64 128 256 512
  do
    install -m 644 -D "icons-sources/icon_${_size}x${_size}.png" \
      "${pkgdir}/usr/share/icons/hicolor/${_size}x${_size}/apps/dbeaver.png"
  done

  # Move into the target directory
  cd "target/products/org.jkiss.dbeaver.core.product/linux/gtk/${CARCH}"

  # Removed different architectures libraries
  for _dir in aix-ppc aix-ppc64 darwin-aarch64 darwin-x86-64 \
    dragonflybsd-x86-64 freebsd-aarch64 freebsd-x86 freebsd-x86-64 \
    linux-aarch64 linux-arm linux-armel linux-loongarch64 linux-mips64el \
    linux-ppc linux-ppc64le linux-riscv64 linux-s390x linux-x86 \
    openbsd-x86 openbsd-x86-64 \
    sunos-sparc sunos-sparcv9 sunos-x86 sunos-x86-64 \
    win32 win32-aarch64 win32-x86 win32-x86-64
  do
    rm -r "dbeaver/plugins/com.sun.jna_5.17.0.v20250316-1700/com/sun/jna/${_dir}"
  done
  # Initially install everything into /usr/lib/dbeaver
  install -m 755 -d "${pkgdir}/usr/lib"
  cp -r "dbeaver" "${pkgdir}/usr/lib/${pkgname}"

  # Move shared data to /usr/share/dbeaver
  cd "${pkgdir}/usr/lib/${pkgname}"
  install -m 755 -d "${pkgdir}/usr/share/${pkgname}"
  for _file in configuration features p2 .eclipseproduct artifacts.xml dbeaver.ini readme.txt
  do
    mv "${_file}" "${pkgdir}/usr/share/${pkgname}"
    ln -s "/usr/share/${pkgname}/${_file}" .
  done

  # Install additional licenses
  install -m 755 -d "${pkgdir}/usr/share/licenses"
  mv licenses "${pkgdir}/usr/share/licenses/${pkgname}"
  ln -s "/usr/share/licenses/${pkgname}" "${pkgdir}/usr/lib/${pkgname}/licenses"

  # Install icons
  install -m 755 -d "${pkgdir}/usr/share/pixmaps"
  mv dbeaver.png "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
  mv icon.xpm "${pkgdir}/usr/share/pixmaps/${pkgname}.xpm"

  # Install executable script into /usr/bin
  install -m 755 -d "${pkgdir}/usr/bin"
  install -m 755 "${srcdir}/dbeaver.sh" "${pkgdir}/usr/bin/${pkgname}"

  # Install application launcher into /usr/share/applications
  install -m 755 -d "${pkgdir}/usr/share/applications"
  install -m 755 -t "${pkgdir}/usr/share/applications" "${srcdir}/io.${pkgname}.DBeaver.desktop"

  # Clean up and install new profile
  rm -rf "${pkgdir}/usr/share/${pkgname}/p2/org.eclipse.equinox.p2.core"
  cd "${pkgdir}/usr/share/${pkgname}/p2/org.eclipse.equinox.p2.engine/profileRegistry/DefaultProfile.profile"
  find . -name "*.profile.gz" -delete
  install -m 644 "${srcdir}/${pkgname}.profile-${pkgver}.gz" "1502633007017.profile.gz"
  cd "${pkgdir}/usr/share/${pkgname}/p2/org.eclipse.equinox.p2.engine"
  rm -f ".settings/org.eclipse.equinox.p2.artifact.repository.prefs"
  rm ".settings/org.eclipse.equinox.p2.metadata.repository.prefs"
  rmdir ".settings"

  # Install system hook
  install -m 755 -d "${pkgdir}/usr/share/libalpm/hooks"
  install -m 644 "${srcdir}/${pkgname}.hook" "${pkgdir}/usr/share/libalpm/hooks"

  # Create configuration file (handled by the hook)
  cd "${pkgdir}/usr/share/dbeaver/configuration/org.eclipse.equinox.simpleconfigurator"
  install -m 755 -d "${pkgdir}/etc/${pkgname}/bundles.d"
  mv "bundles.info" "${pkgdir}/etc/${pkgname}/bundles.d/00-${pkgname}.info"
  ln -s "/etc/${pkgname}/bundles.info" .

  # Add extra plugins
  cd "${srcdir}/${pkgname}-${pkgver}/product/repositories/org.jkiss.dbeaver.ce.repository/target/repository"
  _install_plugin "dbeaver-plugin-apache-poi" "org.apache.commons.commons-compress"
  _install_plugin "dbeaver-plugin-apache-poi" "org.jkiss.bundle.apache.poi"
  _install_plugin "dbeaver-plugin-office" "org.jkiss.dbeaver.data.office"
  _install_plugin "dbeaver-plugin-office" "org.jkiss.dbeaver.data.office.ui"
}

_install_plugin() {
  _bundle="$1"
  _plugin="$2"
  _file="$(echo plugins/${_plugin}_*.jar)"
  # Find plugin version from filename
  _version="${_file##*_}"
  _version="${_version%.jar}"
  # Install plugin
  install -m 644 ${_file} "${pkgdir}/usr/lib/${pkgname}/plugins"
  # Add plugin information to bundles
  echo "${_plugin},${_version},${_file},4,false" >> "${pkgdir}/etc/dbeaver/bundles.d/20-${_bundle}.info"
}
