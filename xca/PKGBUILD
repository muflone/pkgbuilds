# Maintainer: Muflone http://www.muflone.com/contacts/english/
# Contributor: Andrea Zucchelli <zukka77@gmail.com>

pkgname=xca
pkgver=2.6.0
pkgrel=1
pkgdesc='Graphical user interface to OpenSSL, RSA/DSA public keys, certificates, signing requests and revokation lists'
arch=('x86_64')
url='https://hohnstaedt.de/xca/'
license=('BSD-3-Clause')
makedepends=('cmake')
depends=('qt6-tools' 'libltdl' 'desktop-file-utils' 'gtk-update-icon-cache')
source=("https://github.com/chris2511/${pkgname}/releases/download/RELEASE.${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('0904df3095cd1cce3c1d19320f207f1997378776728767201a791680d7b937fd947bfdb887cbc7dd4d9ffa18178807cba7844245cf65c84591af7af287ed531e')

build () {
  cd "${pkgname}-${pkgver}"
  cmake -DCMAKE_INSTALL_PREFIX=/usr .
  cmake --build .
}

package() {
  cd "${pkgname}-${pkgver}"
  cmake --install . --prefix "${pkgdir}/usr"
  install -m 644 -D COPYRIGHT "${pkgdir}/usr/share/licenses/${pkgname}/COPYRIGHT"
}
