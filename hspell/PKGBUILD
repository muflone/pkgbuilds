# Maintainer: Muflone http://www.muflone.com/contacts/english/
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgbase=hspell
pkgname=('hspell' 'hunspell-he')
pkgver=1.4
pkgrel=7
arch=('x86_64')
license=('AGPL-3.0-only')
url="http://www.ivrix.org.il/"
makedepends=('glibc' 'zlib' 'perl' 'hunspell' 'gawk' 'qt6-webengine')
options=('!makeflags')
source=(http://hspell.ivrix.org.il/${pkgname}-${pkgver}.tar.gz{,.sig})
sha256sums=('7310f5d58740d21d6d215c1179658602ef7da97a816bc1497c8764be97aabea3'
            'SKIP')
validpgpkeys=('996512CD85DC510BD45EAD46415E26D84D4E37DB')

build() {
  cd ${pkgname}-${pkgver}
  export PERLLIB="${PWD}"
  ./configure --prefix=/usr --mandir=/usr/share/man \
      --enable-linginfo --enable-fatverb --enable-shared
  make 
  make hunspell
}

package_hspell() {
  pkgdesc="Hebrew spell-checker"
  depends=('glibc' 'zlib' 'perl')

  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install

  rm -f "${pkgdir}"/usr/lib/libhspell.a
}

package_hunspell-he() {
  pkgdesc="Hebrew hunspell dictionary"
  provides=('hunspell-dictionary')

  cd ${pkgbase}-${pkgver}
  # Install files
  install -dm755 "${pkgdir}/usr/share/hunspell"
  install -m644 he.dic "${pkgdir}/usr/share/hunspell/he_IL.dic"
  install -m644 he.aff "${pkgdir}/usr/share/hunspell/he_IL.aff"

  # Install symlinks
  install -dm755 "${pkgdir}/usr/share/myspell/dicts"
  pushd "${pkgdir}/usr/share/myspell/dicts"
  for _file in "${pkgdir}/usr/share/hunspell"/*
  do
    ln -sv "/usr/share/hunspell/$(basename ${_file})" .
  done
  popd

  # Install webengine dictionaries   
  install -d "${pkgdir}/usr/share"/qt{,6}/qtwebengine_dictionaries/
  for _file in "${pkgdir}/usr/share"/hunspell*/*.dic
  do
    _filename="$(basename ${_file})"
    /usr/lib/qt6/qwebengine_convert_dict "${_file}" \
      "${pkgdir}/usr/share/qt6/qtwebengine_dictionaries/${_filename/\.dic/\.bdic}"
    ln -rs "${pkgdir}/usr/share/qt6/qtwebengine_dictionaries/${_filename/\.dic/\.bdic}" \
      "${pkgdir}/usr/share/qt/qtwebengine_dictionaries/"
  done
}
