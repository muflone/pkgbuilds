# Maintainer: Muflone http://www.muflone.com/contacts/english/
# Contributor: web.de jan.stuehler

pkgname=perl-pdf-builder
_perl_namespace=PDF
_perl_module=Builder
pkgver=3.028
pkgrel=1
pkgdesc="Facilitates the creation and modification of PDF files"
arch=('any')
url="https://metacpan.org/release/${_perl_namespace}-${_perl_module}"
license=('LGPL-2.1-or-later')
depends=('perl' 'perl-font-ttf')
checkdepends=('perl-test-exception' 'perl-test-memory-cycle')
source=("https://cpan.metacpan.org/authors/id/P/PM/PMPERRY/${_perl_namespace}-${_perl_module}-${pkgver}.tar.gz")
sha256sums=('62c5d1b7481b7b3c12c589244316b13e5cab179f6685be80840b5d0487975a8c')
options=('!emptydirs')

build() {
  cd "${_perl_namespace}-${_perl_module}-${pkgver}"
  unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  perl Makefile.PL
  make
}

check() {
  cd "${_perl_namespace}-${_perl_module}-${pkgver}"
  unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1
  make test
}

package() {
  cd "${_perl_namespace}-${_perl_module}-${pkgver}"
  unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
  make pure_install INSTALLDIRS=vendor DESTDIR="${pkgdir}"
  # Delete unuseful files
}
