# Maintainer: Muflone http://www.muflone.com/contacts/english/

pkgname=perl-data-uuid
_perl_namespace=Data
_perl_module=UUID
pkgver=1.227
pkgrel=1
pkgdesc="Globally/Universally Unique Identifiers (GUIDs/UUIDs)"
arch=('x86_64')
url="https://github.com/bleargh45/${_perl_namespace}-${_perl_module}"
license=('LicenseRef-custom')
depends=('perl')
source=("${pkgname}-${pkgver}.tar.gz"::"https://github.com/bleargh45/Data-UUID/archive/refs/tags/${pkgver}.tar.gz")
sha256sums=('0b8901706a0cedca2c3632ecdc46ec5f20118a4c60406b36e59286acd00043e3')
options=('!emptydirs')

build() {
  cd "${_perl_namespace}-${_perl_module}-${pkgver}"
  unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  perl Makefile.PL
  make
}

check() {
  cd "${_perl_namespace}-${_perl_module}-${pkgver}"
  unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1
  make test
}

package() {
  cd "${_perl_namespace}-${_perl_module}-${pkgver}"
  unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
  make pure_install INSTALLDIRS=vendor DESTDIR="${pkgdir}"
  # Install license file
  install -m 755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m 644 -t "${pkgdir}/usr/share/licenses/${pkgname}" "LICENSE"
}
