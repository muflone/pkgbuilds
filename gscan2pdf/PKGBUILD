# Maintainer: Muflone http://www.muflone.com/contacts/english/
# Contributor: Tristelune  <tristelune at archlinux dot info>
# Contributor: LeCrayonVert <greenarrow at archlinux dot us>
# Contributor: yugrotavele <yugrotavele at archlinux dot us>
# Contributor: Sven Salzwedel <sven_salzwedel@web.de>
# Contributor: Olaf Leidinger <leidola@newcon.de>

pkgname=gscan2pdf
pkgver=2.13.5
pkgrel=1
pkgdesc='A GUI with OCR capability to produce PDFs or DjVus from scanned documents'
arch=('any')
url='http://gscan2pdf.sourceforge.net/'
license=('GPL-3.0-only')
depends=('imagemagick' 'perl-config-general' 'perl-date-calc' 'perl-goocanvas2'
         'perl-gtk3-simplelist' 'perl-data-uuid'
         'perl-filesys-df' 'perl-html-parser' 'perl-list-moreutils'
         'perl-locale-codes' 'perl-locale-gettext' 'perl-log-log4perl' 'perl-pdf-api2'
         'perl-proc-processtable' 'perl-readonly' 'perl-image-sane' 'perl-set-intspan'
         'perl-try-tiny' 'perl-pdf-builder' 'perl-gtk3-imageview' 'unpaper'
         'perl-graphics-tiff')
optdepends=('djvulibre: DjVu image format'
            'gocr: OCR support'
            'xdg-utils: email as PDF'
            'tesseract: OCR support'
            'pdftk: PDF encryption'
            'java-commons-lang: PDF encryption')
source=("https://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.xz"{,.asc})
sha256sums=('0d4304f6723d076f868feb013e3d7be925dfbb10dcdc23177dbcbf6606b7d5fa'
            'SKIP')
validpgpkeys=('463293E4AE33871846F30227B321F203110FCAF3') # Jeffrey Ratcliffe
options=('!emptydirs')

build() {
  cd "${pkgname}-${pkgver}"
  unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  perl Makefile.PL
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  make install INSTALLDIRS=vendor DESTDIR="${pkgdir}"
  mv "${pkgdir}/usr/bin/vendor_perl/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
  # Delete unuseful files
  find "${pkgdir}" -name '.packlist' -delete
  find "${pkgdir}" -name '*.pod' -delete
}
